/*
 * Copyright 2017 ThoughtWorks, Inc.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

apply plugin: 'java'

def gitRevision = { ->
    def hashStdOut = new ByteArrayOutputStream()
    exec {
        commandLine "git", "log", "-n", "1", "--format=%H"
        standardOutput = hashStdOut
    }

    return hashStdOut.toString().trim()
}

def releaseRevision = { ->
    def hashStdOut = new ByteArrayOutputStream()
    exec {
        commandLine "git", "log", "--pretty=format:"
        standardOutput = hashStdOut
    }

    return hashStdOut.size().toString()
}

def DIST_VERSION = releaseRevision()
def GIT_REVISION = gitRevision()
def PLUGIN_VERSION = '1.0.0'

project.ext.distVersion = DIST_VERSION
project.ext.fullVersion = DIST_VERSION ? "${PLUGIN_VERSION}-${DIST_VERSION}" : PLUGIN_VERSION
project.ext.gitRevision = GIT_REVISION

version = project.fullVersion
group = 'cd.go'

project.ext.pluginDesc = [
        id         : 'cd.go.authorization.github',
        version    : project.fullVersion,
        goCdVersion: '17.5.0',
        name       : 'GitHub OAuth authorization plugin',
        description: 'GitHub OAuth authorization plugin for GoCD',
        vendorName : 'GoCD Contributors',
        vendorUrl  : 'https://github.com/gocd/github-authorization-plugin',
]

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    jcenter()
    mavenLocal()
}

dependencies {
    compileOnly group: 'cd.go.plugin', name: 'go-plugin-api', version: '17.5.0'
    compile group: 'com.google.code.gson', name: 'gson', version: '2.8.0'
    compile group: 'org.kohsuke', name: 'github-api', version: '1.86'
    compile group: 'com.squareup.okhttp3', name: 'okhttp', version: '3.8.1'

    testCompile group: 'cd.go.plugin', name: 'go-plugin-api', version: '17.4.0'
    testCompile group: 'junit', name: 'junit', version: '4.12'
    testCompile group: 'org.mockito', name: 'mockito-core', version: '2.2.28'
    testCompile group: 'org.hamcrest', name: 'hamcrest-library', version: '1.3'
    testCompile group: 'org.skyscreamer', name: 'jsonassert', version: '1.4.0'
    testCompile group: 'org.jsoup', name: 'jsoup', version: '1.10.2'
    testCompile 'com.squareup.okhttp3:mockwebserver:3.8.1'
}

processResources {
    from("src/main/resource-templates") {
        filesMatching('plugin.xml') {
            expand project.pluginDesc
        }

        filesMatching('plugin.properties') {
            expand project.pluginDesc
        }
    }
}

tasks.withType(Jar) { jarTask ->
    preserveFileTimestamps = false
    reproducibleFileOrder = true

    ['MD5', 'SHA1', 'SHA-256'].each { algo ->
        jarTask.outputs.files("${jarTask.archivePath}.${algo}")
        jarTask.doLast {
            ant.checksum file: jarTask.archivePath, format: 'MD5SUM', algorithm: algo
        }
    }

    manifest {
        attributes(
                'Go-Version': project.pluginDesc.goCdVersion,
                'Plugin-Revision': project.pluginDesc.version,
                'Implementation-Title': project.name,
                'Implementation-Version': project.version,
                'Source-Compatibility': project.sourceCompatibility,
                'Target-Compatibility': project.targetCompatibility
        )
    }
}

tasks.withType(Test) {
    outputs.upToDateWhen { false }
    testLogging {
        showStandardStreams = project.path != ':server'

        exceptionFormat 'full'

        beforeSuite { suite ->
            if (suite.parent) {
                logger.quiet("Running ${suite.name}")
            }
        }

        afterSuite { suite, result ->
            if (suite.parent) {
                logger.quiet("Tests run: ${result.testCount}, Failures: ${result.failedTestCount}, Skipped: ${result.skippedTestCount}, Time elapsed: ${(result.endTime - result.startTime) / 1000.00} sec")
                if (result.resultType == TestResult.ResultType.FAILURE) {
                    logger.quiet("Test ${suite.name} FAILED")
                }
            } else {
                logger.quiet("Total tests run: ${result.testCount}, Failures: ${result.failedTestCount}, Skipped: ${result.skippedTestCount}, Time elapsed: ${(result.endTime - result.startTime) / 1000.00} sec")
            }
        }
    }
}

sourceSets {
    test {
        java {
            compileClasspath += configurations.compileOnly
            runtimeClasspath += configurations.compileOnly
        }
    }
}

jar {
    from(configurations.compile) {
        into "lib/"
    }

//    This is useful for debugging
//    from(sourceSets.main.java) {
//        into "/"
//    }
}

//
//task deploy(type: Copy) {
//    from jar
//    into "../../../gocd/server/plugins/external"
//}